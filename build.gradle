plugins {
    id 'com.enonic.xp.app' version '3.5.2'
    id "com.github.node-gradle.node" version "7.1.0"
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo('dev')
}

node {
    download = true
    version = '20.13.1'
}

app {
    name = "${appName}"
    displayName = "${appDisplayName}"
    vendorName = "${vendorName}"
    vendorUrl = "${vendorUrl}"
    systemVersion = "${xpVersion}"
}

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    //include "com.enonic.xp:lib-auth:${xpVersion}"
    //include "com.enonic.xp:lib-context:${xpVersion}"
    //include "com.enonic.xp:lib-i18n:${xpVersion}"
    //include "com.enonic.xp:lib-io:${xpVersion}"
    //include "com.enonic.xp:lib-mail:${xpVersion}"
    //include "com.enonic.xp:lib-repo:${xpVersion}"
    include "com.enonic.xp:lib-schema:${xpVersion}"
    //include "com.enonic.xp:lib-websocket:${xpVersion}"

    // The version numbers must be here (or dependabot will fail)
    include 'com.enonic.lib:lib-asset:1.0.1'
    include 'com.enonic.lib:lib-thymeleaf:2.1.1'
    include 'com.enonic.lib:lib-guillotine:6.2.1'

    include 'com.enonic.lib:lib-react4xp:6.0.0-B3'
}

sourceSets {
  main {
    resources {
      ['.gitkeep','*.es6','*.jsx','*.scss','*.tsx'].each { ext ->
        exclude "**/$ext"
      }
    }
  }
}

processResources {
  // Files that are simply copied by gradle: png svg xml
  exclude '**/.gitkeep'
  exclude '**/*.jsx' // handled by @enonic/react4xp gradle files
  exclude '**/*.sass' // handled by @enonic/react4xp gradle files
  exclude '**/*.scss' // handled by @enonic/react4xp gradle files
  exclude '**/*.tsx' // handled by @enonic/react4xp gradle files
  exclude '**/*.ts' // handled by tsup
}

tasks.withType(Copy).configureEach {
    includeEmptyDirs = false
}

////////////////////////////////////////////////////////////////////////////////////////////////////

tasks.register('npmCheck', NpmTask) {
	dependsOn npmInstall
	args = [
		'run',
		'check'
	]
	environment = [
		'FORCE_COLOR': 'true',
	]
}

check.dependsOn npmCheck

// This task takes care of XP non-react (and therefore non-react4xp) ts files
// under src/main/resources. This is independent of react4xp, so you can replace
// this with your own build steps if you want.
// IMPORTANT:
// This task only handles .ts files under src/main/resources.
// If you use your own logic to compile non-react XP files
// (i.e. replace this task or add more to it),
// you should usually make it ignore .tsx and .jsx files,
// to ensure that those are handled only by the react4xp build
// (node_modules/@enonic/react4xp/react4xp.gradle, aka. react4xpGradlePath)
// and not compiled twice.
tasks.register('buildXpResources', NpmTask) {
    dependsOn( npmInstall )
    args = ['run', 'build:xp:resources']
    group 'enonic xp'
    description 'tsup transpile Enonic XP TypeScript source files (important: ignoring TSX/JSX components since they are left to react4xp)'
    outputs.dir 'build/resources/main'
    // This is just so gradle is able to do incremental builds:
    inputs.files fileTree(dir: 'src/main/resources', exclude: [
      '**/.gitkeep',
      '**/*.cjs',
      '**/*.es',
      '**/*.jsx',
      '**/*.html',
      '**/*.png',
      '**/*.sass',
      '**/*.scss',
      '**/*.svg',
      '**/*.d.ts',
      '**/*.tsx',
      '**/*.xml'
    ])
}

tasks.register('react4xp', NpmTask) {
    args = [
        'run', 'build:react4xp'
    ]
    dependsOn( npmInstall )
    description 'Compile react4xp resources'
    environment = [
        'R4X_APP_NAME': "${appName}",
        'R4X_BUILD_LOG_LEVEL': gradle.startParameter.logLevel.toString(),
        'R4X_DIR_PATH_ABSOLUTE_PROJECT': project.projectDir.toString(),
        'NODE_ENV': project.hasProperty('dev') || project.hasProperty('development') ? 'development' : 'production'
    ]
    group 'react4xp'
    inputs.dir 'node_modules/@enonic/react4xp'
    inputs.dir 'src/main/resources'
    outputs.dir 'build/resources/main'
}

tasks.register('dev', Exec) {
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'gradlew.bat', 'deploy', '-t'
    } else {
        commandLine './gradlew', 'deploy', '-t'
    }
}

jar.dependsOn ( buildXpResources, react4xp )
