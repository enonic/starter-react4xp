plugins {
  id "com.github.node-gradle.node"
}

task react4xp(type: NpmTask) {
  args = [
    'explore', '@enonic/react4xp', '--',
    'npm', 'run', 'build:react4xp'
  ]
  dependsOn npmInstall
  description 'Compile react4xp resources'
  environment = [
    'DIR_PATH_ABSOLUTE_PROJECT': project.projectDir.toString(),
    'GRADLE_LOG_LEVEL': gradle.startParameter.logLevel.toString(),
    'NODE_ENV': project.hasProperty('dev') || project.hasProperty('development') ? 'development' : 'production'
  ]
  group 'React4xp'
  // It also watches package.json and package-lock.json :)
  inputs.dir 'node_modules/@enonic/react4xp'
  inputs.dir 'src/main/resources'
  outputs.dir 'build/resources/main'
}
jar.dependsOn += 'react4xp'


StartParameter startParameter = project.gradle.startParameter;
[
    deploy,
    build,
    check,
    test,
    compileTestJava,
    processTestResources,
    assemble,
    jar,
    classes,
    compileJava,
    processResources,
    npmInstall,
    npmSetup,
    nodeSetup,
    clean
].each { task ->

  // This will make sure that various tasks are not run on when starting continuous build.
  // However once a task is triggered by file change, it will still execute it's dependencies ;(
  task.onlyIf { !startParameter.isContinuous() }

  // This should skip a task even if it's "executed"
  task.doFirst {
    if (startParameter.isContinuous()) { throw new StopExecutionException() }
  }

}
